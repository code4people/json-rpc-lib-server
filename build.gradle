group 'com.code4people'
version '0.3.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    // mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.code4people', name: 'json-rpc-lib-binding', version: '0.3.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.22.0'
}

test {
    reports {
        junitXml.enabled = true
    }

    //exclude 'integrationtests'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task createPom << {
    pom {
        project {
            name 'json-rpc-lib-server'
            packaging 'jar'
            description 'Client library for JSON-RPC'
            url 'https://github.com/code4people/json-rpc-lib-server'

            scm {
                connection 'scm:git:git://github.com/code4people/json-rpc-lib-server.git'
                developerConnection 'scm:git:ssh://github.com/code4people/json-rpc-lib-server.git'
                url 'https://github.com/code4people/json-rpc-lib-server'
            }

            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'mobratil'
                    name 'Martin ObrÃ¡til'
                    email 'martin.obratil@icloud.com'
                    organization 'code4people'
                    organizationUrl 'https://www.code4people.com'
                }
            }
        }
    }.writeTo("$buildDir/poms/pom-default.xml")
}